#!/usr/bin/env coffee

# CLI for MUI development
# -----------------------
# Start site / spec server -> mui -s, mui --server
# Run specs                -> mui -t, mui --test
# Generate documentation   -> mui -d, mui --doc
# Build release            -> mui -b, mui --build

nconf = require 'nconf'
nconf.argv
  s:
    alias: 'server'
    describe: 'Starts site / pec server.'
  d:
    alias: 'doc'
    describe: 'Generate documentation.'
  t:
    alias: 'test'
    describe: 'Runs specs on saucelabs.'
  b:
    alias: 'build'
    describe: 'Build release files.'

if nconf.get('doc')
  console.log 'Generating documentation...\n'
  codo = require('codo')
  codo.run()
else if nconf.get('server') or nconf.get('build')
  connect      = require("connect")
  Mincer       = require("mincer")
  autoprefixer = require('autoprefixer')

  class AutoPrefix extends Mincer.Template
    evaluate: (context, locals, callback)->
      callback null, autoprefixer.compile @data

  Mincer.registerPostProcessor('text/css', AutoPrefix)

  environment = new Mincer.Environment()
  environment.appendPath('site')
  environment.appendPath('core')
  environment.appendPath('components')
  environment.appendPath('themes')
  environment.appendPath('polyfills')
  environment.appendPath('specs')

  if nconf.get('server')
    app = connect()
    app.use "/", connect.static process.cwd()+"/site"
    app.use connect.static process.cwd()
    app.use "/assets", Mincer.createServer(environment)
    app.listen(process.env.PORT || 4000)

    console.log 'MUI development server running on port 4000...'
  else
    fs = require 'fs'
    unless fs.existsSync('release')
      console.log 'Creating release directory...'
      fs.mkdirSync('release')
    console.log 'Compiling source...'
    environment.findAsset('mui.js').compile (err, asset) ->
      code = asset.toString()
      fs.writeFileSync('release/mui.js', code, 'utf-8')
      console.log 'Compiling default theme...'
      environment.findAsset('default/theme.css').compile (err, asset) ->
        code = asset.toString()
        fs.writeFileSync('release/theme.css', code, 'utf-8')
        console.log 'Done'

else if nconf.get('test')
  request   = require "request"
  async     = require "async"
  wd        = require "wd"
  Q         = require "q"

  host      = "localhost"
  port      = 4445
  username  = process.env.SAUCE_USERNAME
  accessKey = process.env.SAUCE_ACCESS_KEY
  browser   = wd.promiseRemote(host, port, username, accessKey)

  OSX10_8  = 'OS X 10.8'
  OSX10_6  = 'OS X 10.6'
  WIN7     = 'Windows 7'
  WINXP    = 'Windows XP'
  WIN8     = 'Windows 8'
  LINUX    = 'Linux'
  TEST_URL = "http://localhost:4000/specs/"

  browsers = {
    'android|4':            [LINUX]
    'android|4|tablet':     [LINUX]
    'iphone':               [OSX10_8]
    'ipad':                 [OSX10_8]
    'internet explorer|10': [WIN8]
    'internet explorer|9':  [WIN7]
    'firefox|21':           [WIN8,WIN7,WINXP,LINUX,OSX10_6]
    'opera|12':             [WIN7,WINXP,LINUX]
    'safari|5':             [WIN7,OSX10_6]
    'safari|6':             [OSX10_8]
    'chrome':               [WIN7,WINXP,LINUX,OSX10_6]
  }

  api = (url, method, data) ->
    deferred = Q.defer()
    request
      method: method
      uri: ["https://", username, ":", accessKey, "@saucelabs.com/rest", url].join("")
      headers: { "Content-Type": "application/json" }
      body: JSON.stringify(data)
    , (error, response, body) -> deferred.resolve response.body
    deferred.promise

  failed = 0
  tests = []

  for bw, platforms of browsers
    do (bw, platforms) ->
      [bw,version,deviceType] = bw.split('|')
      platforms.forEach (platform) ->
        tests.push (callback)->
          console.log bw+" - "+platform
          opts =
            browserName: bw
            platform: platform
            name: "MUI - Modern User Interfaces"
            build: process.env.TRAVIS_BUILD_NUMBER
            "tunnel-identifier": process.env.TRAVIS_JOB_NUMBER
          if deviceType
            opts.deviceType = deviceType
          if version
            opts.version = version
          browser.init(opts).then(->
            browser.get(TEST_URL).delay 3000
          ).then(->
            browser.eval "window.results"
          ).then((results) ->
            unless results
              passed = false
              failed = 1
            else
              passed = results.failed is 0
              failed = 1 unless passed

            data =
              passed: passed
              "custom-data":
                test: results

            api ["/v1/", username, "/jobs/", browser.sessionID].join(""), "PUT", data
          ).fin(->
            browser.quit()
          ).then(->
            callback()
          ).done()

  tests.push -> process.exit failed
  async.series(tests)
